// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Put you code here:

    Mux16(a=x, b[0..15]=false, sel=zx, out=xZx);  // choose whether to zero the x
    Not16(in=xZx, out=notXZx);  // not the zeroed x
    Mux16(a=xZx, b=notXZx, sel=nx, out=xForF);  //choose whether to not the zeroed x

    Mux16(a=y, b[0..15]=false, sel=zy,out=yZy);  // choose whether to zero the y
    Not16(in=yZy, out=notYZy);  // not the zeroed y
    Mux16(a=yZy, b=notYZy, sel=ny, out=yForY);  // choose whether to not the zeroed y

    Add16(a=xForF, b=yForY, out=xPlusY);  // x+y
    And16(a=xForF, b=yForY, out=xAndY);  // x&y
    Mux16(a=xAndY, b=xPlusY, sel=f, out=fXY);  // choxse whether x+y or x&y

    Not16(in=fXY, out=notFXY);  // not the fXY
    Mux16(a=fXY, b=notFXY, sel=no, out=tempout);  // choose whether to not the fXY

    Mux16(a=fXY, b=notFXY, sel=no, out[0..7]=lowOut, out[8..15]=highOut);  // choose whether to not the fXY
    Or8Way(in=lowOut, out=lowNotZr);  // compute the notZr by two Or8Way, since we do not have Or16Way yet
    Or8Way(in=highOut, out=highNotZr);
    Or(a=lowNotZr, b=highNotZr, out=notZr);

    Not(in=notZr, out=zr);  // not the notZr

    And16(a[0..15]=true, b=tempout, out[15]=ng, out[0..14]=drop); // ng = And(true, _out)[15]

    Or16(a=tempout, b[0..15]=false, out=out);  // oo=output

}